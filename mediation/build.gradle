//==================================================================================================
// PROJECT
//==================================================================================================
version=project.version?:"BETA"
group=project.group?:"net.pubnative"
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        // BINTRAY
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        // COVERALLS
        classpath 'net.saliman:gradle-cobertura-plugin:2.3.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3'
    }
}
task projectInfo << {
    println ""
    println " === ${project.group}:${project.name}:${version} - (${System.getenv("CIRCLE_BUILD_NUM")}) ==="
    println ""
}
//==================================================================================================
// ANDROID
//==================================================================================================
apply plugin: 'com.android.library'
android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 10
        versionCode System.getenv("CIRCLE_BUILD_NUM")?System.getenv("CIRCLE_BUILD_NUM").toInteger():1
        versionName project.version?:"BETA"
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'net.pubnative:library:2.2.1'
    compile 'net.pubnative:library.interstitial:2.2.1'
    compile 'net.pubnative:library.feed.banner:2.2.1'
    compile 'net.pubnative:library.banner:2.2.1'
    compile 'net.pubnative:library.video:2.2.1'
    compile 'net.pubnative:library.feed.video:2.2.1'
    compile 'net.pubnative:advertising_id_client:1.0.1'

    // JVM tests
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.assertj:assertj-android:1.1.1'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:1.+'
}
//==================================================================================================
// COVERALLS
//==================================================================================================
apply plugin: "net.saliman.cobertura"
apply plugin: "com.github.kt3k.coveralls"
cobertura.coverageFormats = ['html', 'xml']

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

//==================================================================================================
// JAR
//==================================================================================================
if (System.getenv("CIRCLECI")) {
    task createJar(type: Copy) {
        from('build/intermediates/bundles/release/')
        into("${System.getenv("CIRCLE_ARTIFACTS")}/")
        include('classes.jar')
        rename('classes.jar', "${project.name}_${version}.jar")
    }

    task deleteJar(type: Delete) {
        delete "${System.getenv("CIRCLE_ARTIFACTS")}/${project.name}_${version}.jar"
    }

    createJar.dependsOn(deleteJar, build)
}