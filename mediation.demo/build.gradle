//==================================================================================================
// PROJECT
//==================================================================================================
version=project.version?:"BETA"
group=project.group?:"net.pubnative"
buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        // FABRIC
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
task projectInfo << {
    println ""
    println " === ${project.group}:${project.name}:${version} - (${System.getenv("CIRCLE_BUILD_NUM")}) ==="
    println ""
}
//==================================================================================================
// ANDROID
//==================================================================================================
apply plugin: 'com.android.application'
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "net.pubnative.mediation.demo"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode System.getenv("CIRCLE_BUILD_NUM")?System.getenv("CIRCLE_BUILD_NUM").toInteger():1
        versionName project.version?:"BETA"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile project(':mediation')
    // FACEBOOK
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.facebook.android:audience-network-sdk:4.11.0'
    // FLURRY
    compile 'com.flurry.android:analytics:6.2.0'
    compile 'com.flurry.android:ads:6.2.0'
    //  ADMOB
    compile 'com.google.firebase:firebase-core:9.0.0'
    compile 'com.google.firebase:firebase-ads:9.0.0'
}
//==================================================================================================
// FABRIC
//==================================================================================================
if (System.getenv("CIRCLECI")) {
    apply plugin: 'io.fabric'
    println ""
    println "======== FABRIC ========"
    try {
        File file = new File("${project.projectDir.absolutePath}/fabric.properties")
        if (!file.canRead()) {
            file.createNewFile()
            file << "apiKey=${System.getenv("FABRIC_API_KEY")}\n"
            file << "apiSecret=${System.getenv("FABRIC_API_SECRET")}\n"
        }
        println " - Success"
        println "========================"
        println ""
    } catch (IOException ioException) {
        println " - Error: "
        println " ${ioException.toString()}"
        println "========================"
        println ""
    }
}

apply plugin: 'com.google.gms.google-services'
